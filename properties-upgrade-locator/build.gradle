import de.undercouch.gradle.tasks.download.Download

buildscript {
	dependencies {
		classpath group: "biz.aQute.bnd", name: "biz.aQute.bnd.gradle", version: "3.4.0"
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.source.formatter", version: "latest.release"
		classpath group: "de.undercouch", name: "gradle-download-task", version: "3.3.0"
		classpath group: "net.saliman", name: "gradle-properties-plugin", version: "1.4.6"
	}

	repositories {
		mavenCentral()
	}
}

apply plugin: 'biz.aQute.bnd.builder'
apply plugin: 'com.liferay.source.formatter'
apply plugin: 'de.undercouch.download'
apply plugin: 'java'
apply plugin: 'net.saliman.properties'

repositories {
	mavenCentral()
}

compileJava {
	options.fork = true

	if (java_home) {
		options.forkOptions.executable = new File(java_home, "bin/javac")
	}
	else {
		throw new GradleException('You must set your JAVA_HOME path in gradle.properties file.')
	}

	options.compilerArgs << "-XDignore.symbol.file"
}

dependencies {
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "2.20.0"

	testCompile group: "junit", name: "junit", version: "4.12"
}

formatSource {
	maxLineLength = 120
}

sourceSets {
	main {
		java {
			srcDirs = ['src']
		}
	}
	test {
		java {
			srcDirs = ['test']
		}
	}
}

task downloadPortal(type: Download) {
	src 'https://cdn.lfrs.sl/releases.liferay.com/portal/7.0.3-ga4/liferay-ce-portal-tomcat-7.0-ga4-20170613175008905.zip'
	dest new File("${System.getProperty('user.home')}/.liferay/bundles", 'liferay-ce-portal-tomcat-7.0-ga4-20170613175008905.zip')
	onlyIfNewer true
}

task unzipPortal(dependsOn: downloadPortal, type: Copy) {
	from zipTree(downloadPortal.dest)
	into buildDir
}

test {
	dependsOn unzipPortal

	systemProperty 'liferay.home', "${buildDir}/liferay-ce-portal-7.0-ga4"
}